dnl Process this file with autoconf to produce a configure script.

dnl This file is part of winsup/mingw
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

AC_PREREQ([2.59])
AC_INIT([MinGW Runtime], [v3.x], [http://mingw.org/reporting_bugs], [mingwrt])
MINGW_AC_CONFIG_SRCDIR([__MINGW32_VERSION], [include/_mingw.h])

AC_CANONICAL_SYSTEM
GCC_NO_EXECUTABLES
AC_PROG_CC

case "$with_cross_host" in
  ""|*cygwin*) all_dlls_host='all_dlls_host'
	       install_dlls_host='install_dlls_host';;
esac
AC_SUBST([with_cross_host])
AC_SUBST([all_dlls_host])
AC_SUBST([install_dlls_host])

AC_CHECK_TOOL([AR],      [ar],      [ar])
AC_CHECK_TOOL([AS],      [as],      [as])
AC_CHECK_TOOL([RANLIB],  [ranlib],  [ranlib])
AC_CHECK_TOOL([LD],      [ld],      [ld])
AC_CHECK_TOOL([NM],      [nm],      [nm])
AC_CHECK_TOOL([OBJCOPY], [objcopy], [objcopy])
AC_CHECK_TOOL([DLLTOOL], [dlltool], [dlltool])
AC_CHECK_TOOL([DLLWRAP], [dlltool], [dlltool])
AC_CHECK_TOOL([WINDRES], [windres], [windres])

AC_SUBST([NO_CYGWIN])
AC_SUBST([HEADER_SUBDIR])
AC_SUBST([W32API_INCLUDE], ['-I ${top_srcdir}/../w32api/include'])

# The following are used by the GCC profiling hooks...
#
AC_SUBST([LIBGMON_A], [libgmon.a])
AC_SUBST([CRT0S], ["gcrt1.o gcrt2.o"])

# FIXME: Why is this case determined on `target_os'?
# There is no code generator to be built here, so `target_os'
# should be irrelevant.  Should it not rather be `host_os'?
#
case "$target_os" in
*cygwin*)
  PACKAGE_TARNAME="mingw-runtime"
  NO_CYGWIN="`cd ${srcdir}/../utils; pwd`/mingw"
  # Install mingw headers in mingw subdirectory.
  HEADER_SUBDIR="mingw"
  CRT0S="gcrt0.o"
  ;;
esac

# THREAD_DLL used to be set within the above case and was mingwc for CRTDLL.
# I can find no reason to do this.
#
# FIXME: In the future I would like to change the dll name to mingwthrd to
# to match the libmingwthrd.a name.
AC_SUBST([THREAD_DLL], [mingwm])

# LIBM_A also used be set in the preceding case statement; it had the
# exact same assignment, (libm.a), in every case, so we may just as well
# assign it unconditionally, (making the default case redundant).
#
AC_SUBST([LIBM_A], [libm.a])

AC_SUBST([MKINSTALLDIRS], [$ac_aux_dir/mkinstalldirs])

AC_PROG_INSTALL
MINGW_AC_MANPAGE_TRANSFORM

AC_CONFIG_FILES([Makefile mingwex/Makefile profile/Makefile])
AC_OUTPUT

# $RCSfile$: end of file
