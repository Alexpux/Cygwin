dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.13)

AC_INIT(bzlib.h)
AC_CONFIG_AUX_DIR(`cd $srcdir/../..;pwd`)
AM_INIT_AUTOMAKE(libbz2, 0.0)
AM_MAINTAINER_MODE

# FIXME: We temporarily define our own version of AC_PROG_CC.  This is
# copied from autoconf 2.12, but does not call AC_PROG_CC_WORKS.  We
# are probably using a cross compiler, which will not be able to fully
# link an executable.  This should really be fixed in autoconf
# itself.

AC_DEFUN(LIB_AC_PROG_CC,
[AC_BEFORE([$0], [AC_PROG_CPP])dnl
AC_CHECK_TOOL(CC, gcc, gcc)
if test -z "$CC"; then
  AC_CHECK_PROG(CC, cc, cc, , , /usr/ucb/cc)
  test -z "$CC" && AC_MSG_ERROR([no acceptable cc found in \$PATH])
fi

AC_PROG_CC_GNU

if test $ac_cv_prog_gcc = yes; then
  GCC=yes
dnl Check whether -g works, even if CFLAGS is set, in case the package
dnl plays around with CFLAGS (such as to build both debugging and
dnl normal versions of a library), tasteless as that idea is.
  ac_test_CFLAGS="${CFLAGS+set}"
  ac_save_CFLAGS="$CFLAGS"
  CFLAGS=
  AC_PROG_CC_G
  if test "$ac_test_CFLAGS" = set; then
    CFLAGS="$ac_save_CFLAGS"
  elif test $ac_cv_prog_cc_g = yes; then
    CFLAGS="-gstabs+ -O2"
  else
    CFLAGS="-O2"
  fi
  if test "$ac_test_CXXFLAGS" != set; then
    CXXFLAGS='$(CFLAGS)'
  fi
else
  GCC=
  test "${CFLAGS+set}" = set || CFLAGS="-g"
fi
])

CPPFLAGS=-U_WIN32
AC_CANONICAL_HOST
AC_CHECK_TOOL(AR, ar, ar)
AC_CHECK_TOOL(NM, nm, nm)
AC_CHECK_TOOL(RANLIB, ranlib, ranlib)
LIB_AC_PROG_CC
AC_PROG_INSTALL
AC_STDC_HEADERS
AC_EXEEXT
AC_OBJEXT

AC_SUBST(installdata)dnl
AC_SUBST(uninstalldata)dnl

AC_OUTPUT([Makefile])
